buildscript {
    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath("com.marklogic:ml-data-hub:${mlDHFVersion}")
    }
}
plugins {
    // this plugin lets you create properties files
    // for multiple environments... like dev, qa, prod
    id 'net.saliman.properties' version '1.4.6'
}

apply plugin: 'com.marklogic.ml-data-hub'

repositories {
    jcenter()
    maven { url "http://developer.marklogic.com/maven2/" }
}

configurations {
    mlcp
}

dependencies {
    mlcp "com.marklogic:mlcp:9.0.9"
    mlcp files("lib")
}

task loadOrders(type: com.marklogic.gradle.task.MlcpTask) {
    classpath = configurations.mlcp
    command = "IMPORT"
    port = Integer.parseInt(mlStagingPort)
    input_file_path = "input/orders/orders.csv"
    input_file_type = "delimited_text"
    document_type = "json"
    transform_module = "/data-hub/5/transforms/mlcp-flow-transform.sjs"
    transform_param = "options={},flow-name=LoadOrders"
    output_collections = "Order"
    output_permissions = "rest-reader,read,rest-writer,update"
    output_uri_suffix = ".json"
    generate_uri = true
}
