plugins {
	id 'com.marklogic.ml-gradle' version '4.4.0'
	id 'java'
	id "com.github.psxpaul.execfork" version "0.2.2"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "io.undertow:undertow-core:2.2.21.Final"
	implementation "io.undertow:undertow-servlet:2.2.21.Final"
	implementation "com.marklogic:ml-javaclient-util:4.4.0"
	implementation 'org.slf4j:slf4j-api:1.7.36'
	implementation 'ch.qos.logback:logback-classic:1.3.5'
	implementation "com.fasterxml.jackson.core:jackson-databind:2.14.1"
	implementation 'com.squareup.okhttp3:okhttp:4.10.0'
}

// See https://github.com/psxpaul/gradle-execfork-plugin for docs.
// Note that the task will stop at the end of the build by default, which is typically the behavior we want when
// running tests.
task runReverseProxyServer(type: com.github.psxpaul.task.JavaExecFork, dependsOn: 'compileJava') {
	description = "Run a reverse proxy so that the Java tests can be verified when talking to a reverse proxy instead of " +
		"directly to MarkLogic"
	classpath = sourceSets.main.runtimeClasspath
	main = "com.marklogic.client.test.ReverseProxyServer"
	workingDir = "$buildDir"
	standardOutput = file("$buildDir/reverse-proxy.log")
	errorOutput = file("$buildDir/reverse-proxy-error.log")
}

task runBlockingReverseProxyServer(type: JavaExec) {
	description = "Run the reverse proxy server so that it blocks and waits for requests; use ctrl-C to stop it. " +
		"This is intended for manual testing with the reverse proxy server. If you wish to enable an HTTPS port that is 443" +
		"or any value less than 1024, you will need to use sudo to run this - e.g. " +
		"sudo ./gradlew runBlockingReverseProxyServer -PrpsHttpsPort=443 ."
	classpath = sourceSets.main.runtimeClasspath
	main = "com.marklogic.client.test.ReverseProxyServer"
	args = [rpsMarkLogicServer, rpsProxyServer, rpsHttpPort, rpsHttpsPort, rpsCustomMappings]
}
