plugins {
    id "net.saliman.properties" version "1.5.1"
    id "java"
    id 'maven-publish'
}

repositories {
    jcenter()
}

sourceCompatibility = "9"
targetCompatibility = "9"

group = 'com.marklogic'

dependencies {
    // Contrary to the marklogic-data-hub-central project, when this project is brought over, the okhttp3 dependencies
    // remain at version 4.4.0. So no need to exclude them and then depend on them explicitly.
    compile(project(":marklogic-data-hub")) {
        // Excluding dependencies we don't need in the connector
        exclude module: "spring-boot"
        exclude module: "spring-boot-autoconfigure"
        exclude module: "spring-integration"
        exclude module: "spring-integration-http"
        exclude module: "mlcp-util"
        exclude module: "jcommander"
        exclude module: "marklogic-data-movement-components"
    }

    compile "org.apache.spark:spark-sql_2.11:${sparkVersion}"
    compile "org.apache.spark:spark-launcher_2.11:${sparkVersion}"
    compile "org.apache.spark:spark-catalyst_2.11:${sparkVersion}"
    compile "org.apache.spark:spark-streaming_2.11:${sparkVersion}"
    compile "org.apache.spark:spark-core_2.11:${sparkVersion}"

    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testCompile "org.junit.platform:junit-platform-runner:${junitPlatformVersion}"

    testImplementation(testFixtures(project(":marklogic-data-hub")))
    // Looks hacky, but seems to be the recommended way to depend on test classes from another project
    testCompile files(project(':marklogic-data-hub').sourceSets.test.output)
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            if (project.hasProperty("mavenUser")) {
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
            url publishUrl
        }
    }
}

tasks.withType(Test){
    systemProperty "mlHost", mlHost
    systemProperty "isDhs", isDhs
    useJUnitPlatform()
}
