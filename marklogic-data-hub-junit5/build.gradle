plugins {
    id 'java-library'
    id 'maven-publish'

    id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

group = 'com.marklogic'

// See https://github.com/snyk/gradle-plugin for docs
snyk {
    severity = 'medium'
    api = snykToken
    autoDownload = true
    autoUpdate = true
}

dependencies {
    api "org.junit.jupiter:junit-jupiter:5.7.1"
    api "org.slf4j:slf4j-api:1.7.30"
    api "org.springframework:spring-test:5.2.15.RELEASE"
    api project(":marklogic-data-hub")

    api ("com.marklogic:marklogic-unit-test-client:1.0.0") {
        // 1.0.0 uses Java Client 4.2.0, so we exclude that
        exclude module: "marklogic-client-api"
    }

    testImplementation(testFixtures(project(":marklogic-data-hub")))
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allJava
}

publishing {
    publications {
        mainJava(MavenPublication) {
            from components.java
        }
        sourcesJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            if (project.hasProperty("mavenUser")) {
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
            url publishUrl
        }
    }
}
