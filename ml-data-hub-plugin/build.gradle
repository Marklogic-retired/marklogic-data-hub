/*
 * Copyright 2012-2019 MarkLogic Corporation
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

buildscript {
    repositories {
        jcenter()
        maven { url 'http://developer.marklogic.com/maven2' }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.9"
    }
}


plugins {
    id 'net.saliman.properties' version '1.4.6'
    id "groovy"
    id "java-gradle-plugin"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7.3"
    id 'org.springframework.boot' version '2.0.6.RELEASE'
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
}

apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = 1.8
targetCompatibility = 1.8

bootJar.enabled = false
jar.enabled = true

repositories {
    jcenter()
    mavenCentral()
    maven { url 'http://repo.spring.io/milestone' }
    maven { url 'http://developer.marklogic.com/maven2' }
}

dependencies {
    compile gradleApi()
    compile (project(':marklogic-data-hub')) {
        exclude group: 'ch.qos.logback'
    }
    compile ('com.marklogic:ml-gradle:3.12.0') {
        exclude group: 'ch.qos.logback'
    }
    testCompile localGroovy()
    testCompile gradleTestKit()
    testCompile 'xmlunit:xmlunit:1.3'
    testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile ('org.springframework.boot:spring-boot-starter-test:2.0.6.RELEASE')  {
        exclude module: "logback-classic"      
    }
}

springBoot {
    mainClassName = 'com.marklogic.hub.ApplicationConfig'
}


test {
    testLogging {
        // Make sure output from
        // standard out or error is shown
        // in Gradle output.
        showStandardStreams = true
    }
    // fullcycle tests also exercise provisioning, so on thie PR removing
    // TODO put fullcycle tests in another project?
    //include 'com/marklogic/gradle/fullcycle/**'
    include 'com/marklogic/gradle/task/**'
    classpath = project.sourceSets.main.runtimeClasspath + project.sourceSets.test.runtimeClasspath
}

task testFullCycle(type: Test) {
    useJUnit {
        include 'com/marklogic/gradle/fullcycle/**'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allJava
    from sourceSets.main.allGroovy
}

bootRun {
    enabled = false
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
          if(project.hasProperty("mavenUser")) {
            credentials {
            username mavenUser
            password mavenPassword
            }
          }
          url publishUrl
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['main']
    pkg {
        repo = 'maven'
        name = 'ml-data-hub'
        desc = 'A gradle plugin for Data Hub tasks'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/marklogic/marklogic-data-hub'
        issueTrackerUrl = 'https://github.com/marklogic/marklogic-data-hub/issues'
    }
}

pluginBundle {
    website = 'http://www.gradle.org/'
    vcsUrl = 'git@github.com:marklogic/marklogic-data-hub.git'
    description = 'ml-data-hub A gradle plugin for Data Hub tasks'
    tags = ['marklogic', 'data hub']

    plugins {
        mldatahubPlugin {
            id = 'com.marklogic.ml-data-hub'
            displayName = 'Gradle tasks for MarkLogic Data Hub'
            description = 'Gradle plugin for configuring and deploying the MarkLogic Data Hub'
            tags = ['marklogic', 'data hub']
        }
    }
}
