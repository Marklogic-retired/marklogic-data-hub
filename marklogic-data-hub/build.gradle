plugins {
    id 'net.saliman.properties' version '1.4.6'
    id 'java'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.2'
    id 'com.marklogic.ml-gradle' version '4.0.4'
    //id 'com.moowork.node' version '1.1.1'
    id "com.github.node-gradle.node" version "2.2.4"
    id 'org.springframework.boot' version '2.1.15.RELEASE'
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id 'com.marklogic.ml-development-tools' version '5.2.0'
}

// Disabling javadoc warnings, as we have hundreds of them, and until we want to do something about them,
// seeing them when running a Gradle task (like publishToMavenLocal) is just useless noise
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

repositories {
    jcenter()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://developer.marklogic.com/maven2/' }

    // Needed for marklogic-junit
    maven { url 'https://dl.bintray.com/marklogic-community/Maven/' }
}

group = 'com.marklogic'

sourceCompatibility = "9"
targetCompatibility = "9"

// Something changed with 5.5.0 such that "test --tests" doesn't work with Gradle 6.2 or 6.5
ext.junitPlatformVersion = '1.4.2'
ext.junitJupiterVersion  = '5.4.2'

dependencies {
    // Forcing use of 1.3.61 - which is what OkHttp 4.4.0 wants - so that 1.2.x versions don't come onto the classpath
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.3.61"
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:1.3.61"

    compile ('com.marklogic:marklogic-client-api:5.2.0') {
        // Inclusion of woodstox causes legacy flow runner tests to fail, so it's being excluded
        exclude group: 'com.fasterxml.woodstox', module: 'woodstox-core'

        // Oddly, the OkHttp 4.4.0 dependency includes version 1.2.70 and 1.3.61 of these dependencies. This causes
        // class conflict errors. So they're excluded from here, and we force inclusion of 1.3.61 above.
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-common'
    }

    compile('com.marklogic:ml-app-deployer:4.0.3'){
        exclude group: 'org.springframework', module: 'http'
        // prefer the version declared above
        exclude group: 'com.marklogic', module: 'marklogic-client-api'
    }

    compile group: 'org.springframework.boot', name: 'spring-boot', version: '2.1.15.RELEASE'
    compile group: 'org.springframework.integration', name: 'spring-integration-http', version: '5.1.11.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: '2.1.15.RELEASE'

    compile 'com.marklogic:mlcp-util:0.9.0'
    compile ('com.marklogic:marklogic-data-movement-components:2.0.0') {
        // prefer the version declared above
        exclude group: 'com.marklogic', module: 'marklogic-client-api'
    }

    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.commons:commons-text:1.1'

    // For installer program
    compile "com.beust:jcommander:1.72"

    // JUnit Jupiter API and TestEngine implementation
    testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testCompile "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testCompile "org.junit.platform:junit-platform-commons:${junitPlatformVersion}"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"

    // Only needed to run tests in an (IntelliJ) IDE(A) that bundles an older version
    testRuntime "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"

    testCompile group: 'org.springframework', name: 'spring-test', version: '5.1.16.RELEASE'

    // For managing a pool of HubConfigImpl objects
    testFixturesApi "org.apache.commons:commons-pool2:2.8.1"

    testFixturesApi 'xmlunit:xmlunit:1.3'
    testCompile 'org.skyscreamer:jsonassert:1.5.0'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile 'org.easymock:easymock:3.4'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'org.slf4j:log4j-over-slf4j:1.7.13'
    testCompile("com.marklogic:mlcp:10.0.4.2") {
      exclude group: 'org.apache.avro', module: 'avro-tools'
      exclude group: 'org.apache.commons', module: 'commons-csv'
    }

    // Provides support for invoking marklogic-unit-test tests via JUnit
    testCompile ("com.marklogic:marklogic-junit5:1.0.0") {
        // prefer the version declared above
        exclude group: 'com.marklogic', module: 'marklogic-client-api'
    }
    mlBundle "com.marklogic:marklogic-unit-test-modules:1.0.0"
}

import com.marklogic.mgmt.ManageClient
import com.marklogic.mgmt.resource.hosts.HostManager


configurations.all {
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
}

// The Spring Boot bootJar task produces an executable jar with all dependencies that runs the DHF installer
// This jar is only meant to be used by the DHS team for installing DHF into DHS
bootJar {
    archiveClassifier = "installer"
    mainClassName = "com.marklogic.hub.dhs.installer.Main"
    from("src/main/installer") {
        include "logback.xml"
    }
}

task clientJar(type: Jar) {
    description = "Build an executable jar to be used by DHF clients; as of 5.2.0, this is just for running flows"
    manifest {
        attributes "Main-Class": "com.marklogic.hub.cli.client.Main"
    }
    archiveClassifier = "client"
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    exclude "spring-boot*.jar"
    from("src/main/clientJar") {
        include "logback.xml"
    }
    with jar
}
build.dependsOn clientJar

node {
  // Version of node to use.
  version = '8.10.0'

  // // Version of npm to use.
  npmVersion = '6.13.1'

  download = true
  distBaseUrl = "${nodeDistributionBaseUrl}"

  // Set the work directory for unpacking node
  workDir = file("${project.buildDir}/nodejs")

  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${project.projectDir}/src/trace-ui")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task cleanUI(type: Delete) {
  delete 'src/main/resources/ml-modules/root/trace-ui'
}

task npmInstallUI(type: NpmTask) {
    args = ['install']
    dependsOn tasks.cleanUI
}

task buildUI(type: NpmTask) {
    args = ['run', 'build.prod']
    dependsOn tasks.npmInstallUI
}

task copyUIAssets(type: Copy) {
    from 'src/trace-ui/dist'
    into 'src/main/resources/ml-modules/root/trace-ui'
    dependsOn tasks.buildUI
}

processResources {
    filesMatching("**/version.properties") {
        expand(project: project)
    }
}

ext {
    dataServicesGroup = "Data Services"
    dataServicesPath = "src/main/resources/ml-modules/root/data-hub/5/data-services"
}

task generateDataService(type: JavaExec) {
    description = "Generate DS source files based on a service name and endpoint name specified via -PserviceName and -PendpointName"
    classpath = sourceSets.test.runtimeClasspath
    main = 'com.marklogic.hub.tools.DataServiceGenerator'
    args = [
        project.hasProperty("serviceName") ? project.findProperty("serviceName") : "",
        project.hasProperty("endpointName") ? project.findProperty("endpointName") : ""
    ]
}

// Dynamically generate a task for each data-service directory, where the task can generate a Java interface for the data service
new File("src/main/resources/ml-modules/root/data-hub/5/data-services").listFiles().each { file ->
    if (file.isDirectory() && new File(file, "service.json").exists()) {
        task "generate${file.getName()}Interface"(type: com.marklogic.client.tools.gradle.EndpointProxiesGenTask, group: dataServicesGroup) {
            description = "Must run this from the ./marklogic-data-hub directory"
            serviceDeclarationFile = dataServicesPath + "/${file.getName()}/service.json"
        }
    }
}

task generateDataServiceInterfaces {
    description = "Generate Java interfaces for all Data Services. Must run this from the ./marklogic-data-hub directory"
    dependsOn {
        tasks.findAll { task -> dataServicesGroup.equals(task.group)}
    }
}

if (!(
    gradle.startParameter.taskNames*.toLowerCase().contains("bootrun") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("test") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("testunit") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("publishplugins") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("publishtomavenlocal") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("bintrayupload") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("javadoc") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("bootjar") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("marklogic-data-hub:bootjar") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("clientjar") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("marklogic-data-hub:clientjar") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("generatedataservice") ||
        // Added for Hub Central
        gradle.startParameter.taskNames*.toLowerCase().contains("bootrun") ||
        gradle.startParameter.taskNames*.toLowerCase().contains("buildrpm") ||

        project.hasProperty('skipui')
)
) {
    processResources.dependsOn copyUIAssets
}

tasks.clean.dependsOn cleanUI

jar{
    enabled = true
}

bootRun {
    enabled = false
}

javadoc {
   options.overview = 'src/main/resources/overview.html'
}

Node pomCustomizations = new NodeBuilder(). project {
    name 'marklogic-data-hub'
    packaging 'jar'
    textdescription 'Library for Creating an Operational Data Hub on MarkLogic'
    url 'https://github.com/marklogic/marklogic-data-hub'

    scm {
        url 'git@github.com:marklogic/marklogic-data-hub.git'
        connection 'scm:git@github.com:marklogic/marklogic-data-hub.git'
        developerConnection 'scm:git@github.com:marklogic/marklogic-data-hub.git'
    }

    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            name 'MarkLogic'
            email 'java-sig@marklogic.com'
            organization 'MarkLogic'
            organizationUrl 'https://www.marklogic.com'
        }
        developer {
            name 'MarkLogic Github Contributors'
            email 'general@developer.marklogic.com'
            organization 'Github Contributors'
            organizationUrl 'https://github.com/marklogic/marklogic-data-hub/graphs/contributors'
        }
    }
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact bootJar
            artifact clientJar

            pom.withXml {
                asNode().appendNode('description', pomCustomizations.textdescription.text())
                asNode().append(pomCustomizations.developers)
                asNode().append(pomCustomizations.name)
                asNode().append(pomCustomizations.packaging)
                asNode().append(pomCustomizations.url)
                asNode().append(pomCustomizations.scm)
                asNode().append(pomCustomizations.licenses)
            }
        }
    }
    repositories {
        maven {
            if (project.hasProperty("mavenUser")) {
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
            url publishUrl
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['main']
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'marklogic'
        desc = pomCustomizations.textdescription.text()
        licenses = ['Apache-2.0']
        websiteUrl = pomCustomizations.url
        vcsUrl = pomCustomizations.scm.url
        issueTrackerUrl = 'https://github.com/marklogic/marklogic-data-hub/issues'
    }
}

ext {
    // mlAppConfig is an instance of com.marklogic.appdeployer.AppConfig
    mlAppConfig {

        moduleTokensPropertiesSources = new ArrayList<>()

        // override some default values with our values
        modulesDatabaseName = mlModulesDbName
        triggersDatabaseName = mlStagingTriggersDbName
        schemasDatabaseName = mlStagingSchemasDbName
        restPort = Integer.parseInt(mlJobPort)

        // Configure custom tokens for our json files
        customTokens.put("%%mlStagingAppserverName%%", mlStagingAppserverName)
        customTokens.put("%%mlStagingPort%%", mlStagingPort)
        customTokens.put("%%mlStagingDbName%%", mlStagingDbName)

        customTokens.put("%%mlFinalAppserverName%%", mlFinalAppserverName)
        customTokens.put("%%mlFinalPort%%", mlFinalPort)
        customTokens.put("%%mlFinalDbName%%", mlFinalDbName)

        customTokens.put("%%mlJobAppserverName%%", mlJobAppserverName)
        customTokens.put("%%mlJobPort%%", mlJobPort)
        customTokens.put("%%mlJobDbName%%", mlJobDbName)

        customTokens.put("%%mlModulesDbName%%", mlModulesDbName)
        customTokens.put("%%mlStagingTriggersDbName%%", mlStagingTriggersDbName)
        customTokens.put("%%mlStagingSchemasDbName%%", mlStagingSchemasDbName)

        modulePaths = [
            new File(getProjectDir(), "src/main/resources/ml-modules").getAbsolutePath(),
            new File(getProjectDir(), "src/test/ml-modules").getAbsolutePath()
        ]
    }
}

def propFile = file("gradle.properties")
boolean sslRun = Boolean.parseBoolean(System.properties['ssl'])
boolean certAuth = Boolean.parseBoolean(System.properties['certauth'])
def props = new Properties()
propFile.withInputStream { props.load(it) }

task bootstrap(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main = 'com.marklogic.bootstrap.TestAppInstaller'
    systemProperty "mlHost", mlHost
}

test {
    systemProperty "mlHost", mlHost
    useJUnitPlatform()
    minHeapSize = "128m"
    maxHeapSize = "256m"
    exclude 'com/marklogic/bootstrap/**'
    classpath = project.sourceSets.test.runtimeClasspath
}

task testAcceptance(type: Test) {
    systemProperty "mlHost", mlHost
    useJUnitPlatform  {
        include 'com/marklogic/hub_unit_test/RunMarkLogicUnitTestsTest*'
        include 'com/marklogic/hub/**'
        if (!(certAuth || sslRun)){
            include 'com/marklogic/hub/PiiE2E.class'
        }
    }
}

task testIntegration(type: Test) {
    systemProperty "mlHost", mlHost
    useJUnitPlatform  {
        include 'com/marklogic/hub_integration/**'
    }
}

task testBootstrap(type: Test) {
    useJUnitPlatform  {
        include 'com/marklogic/bootstrap/**'
    }
}

task testUnit(type: Test) {
    description = "Run the marklogic-unit-test tests, which also forces those modules to be loaded"
    useJUnitPlatform {
        include 'com/marklogic/hub_unit_test/RunMarkLogicUnitTestsTest*'
    }
}

task setupSSL{
    doFirst{
        def bootStrapHost = null
        if(sslRun || certAuth){
            com.marklogic.mgmt.ManageClient manageClient = new ManageClient(new com.marklogic.mgmt.ManageConfig(mlHost, 8002, mlSecurityUsername, mlSecurityPassword))
            com.marklogic.mgmt.resource.hosts.HostManager hostManager = new HostManager(manageClient)
            bootStrapHost = hostManager.getHostNames().get(0)
            if(! bootStrapHost.toLowerCase().contains("marklogic.com")){
                throw new GradleException("The test with current options will run only in marklogic.com domain")
            }

            javaexec {
                classpath = sourceSets.test.runtimeClasspath
                main = 'com.marklogic.bootstrap.SSLsetup'
                args = [mlHost, mlSecurityUsername, mlSecurityPassword, certAuth, sslRun]
                }
            }
            if(sslRun) {
                //checking if one of the property is already set
                if(props.getProperty('mlJobSimpleSsl')==null){
                    propFile.append("\n" +
                            "mlJobSimpleSsl=true\n" +
                            "mlFinalSimpleSsl=true\n" +
                            "mlAdminScheme=https\n" +
                            "mlManageScheme=https\n" +
                            "mlAppServicesSimpleSsl=true\n" +
                            "mlManageSimpleSsl=true\n" +
                            "mlStagingSimpleSsl=true")
                }
            }
            if(certAuth) {
                if(props.getProperty('mlStagingAuth')==null){
                    propFile.append("\n" +
                            "mlStagingAuth=certificate\n" +
                            "mlFinalAuth=certificate\n" +
                            "mlHost="+bootStrapHost+"\n"+
                            "mlAdminScheme=https\n" +
                            "mlManageScheme=https\n" +
                            "mlJobAuth=certificate")
            }
        }
    }
}

bootstrap.finalizedBy mlPrepareBundles // ensures that marklogic-unit-test is downloaded

setupSSL.dependsOn(testClasses)
bootstrap.dependsOn(setupSSL)
task bootstrapAndTest(dependsOn:["bootstrap", "test"])
test.mustRunAfter bootstrap
testIntegration.dependsOn(bootstrap)
testAcceptance.dependsOn(bootstrap)
testBootstrap.dependsOn(bootstrap)
