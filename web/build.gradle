/*
 * Copyright 2012-2019 MarkLogic Corporation
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */

import java.util.concurrent.Executors

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id "com.github.node-gradle.node" version "3.2.1"
    id 'org.springframework.boot' version '2.6.7'
}
apply plugin: 'maven-publish'
apply plugin: 'war'


war {
  baseName = 'marklogic-datahub'
  version = "${version}"
  manifest {
    attributes("Implementation-Title": "Web Services and UI",
      "Implementation-Version": version)
  }

}
mainClassName = "com.marklogic.hub.web.WebApplication"
bootWar {
  baseName "marklogic-datahub"
}

repositories {
    mavenCentral()
    maven {url 'https://developer.marklogic.com/maven2/'}
    maven {url 'https://repo.spring.io/plugins-release/'}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.junitPlatformVersion = '1.8.2'
ext.junitJupiterVersion  = '5.8.2'

dependencies {

    implementation group: 'org.springframework.security', name: 'spring-security-core', version: '5.6.3'
    implementation group: 'org.springframework.security', name: 'spring-security-config', version: '5.6.3'
    implementation group: 'org.springframework.security', name: 'spring-security-web', version: '5.6.3'
    implementation group: 'org.springframework', name: 'spring-web', version: '5.3.19'
    implementation group: 'org.springframework', name: 'spring-webmvc', version: '5.3.19'
    implementation group: 'org.springframework', name: 'spring-websocket', version: '5.3.19'
    implementation('org.springframework.boot:spring-boot-starter-websocket:2.6.7')
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf:2.6.7")

    implementation("org.aspectj:aspectjweaver:1.9.9.1")
    implementation("org.springframework:spring-messaging:5.3.19")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.6.21"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-common:1.6.21"
    implementation(project(":marklogic-data-hub"))
    implementation("org.apache.commons:commons-csv:1.9.0")

    // Optional Boot library - see https://docs.spring.io/spring-boot/docs/current/reference/html/using-boot-devtools.html
    implementation("org.springframework.boot:spring-boot-devtools:2.6.7")
    implementation 'com.marklogic:ml-app-deployer:4.3.3'
    implementation group: 'io.opentracing', name: 'opentracing-api', version: '0.33.0'
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'

    // Needed for the Upload feature using mlcp
    implementation("com.marklogic:mlcp:10.0.9") {
        exclude group: 'org.apache.avro', module: 'avro-tools'
        exclude group: 'javax.servlet'
    }
    implementation 'com.h2database:h2:2.1.212'

    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'

    implementation 'org.springframework.session:spring-session-core:2.6.3'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:2.6.7'
    implementation 'org.springframework.session:spring-session-jdbc:2.6.3'

    //spring boot test libs
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.6.7'
    testImplementation 'org.springframework.security:spring-security-test:5.6.3'
    // JUnit Jupiter API and TestEngine implementation
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"

    testImplementation "org.junit.platform:junit-platform-runner:${junitPlatformVersion}"
    testImplementation 'io.rest-assured:rest-assured:5.0.1'
    testImplementation files(project(':marklogic-data-hub').sourceSets.test.output)
}

evaluationDependsOn(':marklogic-data-hub')
compileTestJava.dependsOn tasks.getByPath(':marklogic-data-hub:testClasses')

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

node {
  // Version of node to use.
  version = '14.15.5'
  // Version of npm to use.
  npmVersion = '6.14.17'

  download = true

  distBaseUrl = "https://nodejs.org/dist"

  // Set the work directory for unpacking node
  workDir = file("${project.buildDir}/nodejs")

  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${project.projectDir}")
}

task cleanUI(type: Delete) {
  delete 'src/main/resources/static', 'src/main/resources/templates'
}

task npmInstallUI(type: NpmTask) {
  args = ['install']
  dependsOn tasks.cleanUI
}

task buildUI(type: NpmTask) {
  args = ['run', 'build.prod']
  dependsOn tasks.npmInstallUI
}

task copyUIAssets(type: Copy) {
  from 'dist'
  into 'src/main/resources/static'
  exclude '*.html'
  dependsOn tasks.buildUI
}

task copyIndexHtml(type: Copy) {
  from 'dist'
  into 'src/main/resources/templates'
  include '*.html'
  dependsOn tasks.copyUIAssets
}

task runUI(type: NpmTask) {
  args = ['run', 'start']
  dependsOn tasks.npmInstallUI
}

task runUIMocks(type: NpmTask) {
  args = ['run', 'startmocks']
  dependsOn tasks.npmInstallUI
}

task startRest {
  doFirst {
    def executorService = Executors.newSingleThreadExecutor()
    executorService.submit({
      tasks.findMainClass.execute()
      pid = tasks.bootRun.execute()

    } as Runnable)
  }
}

task e2eLaunch(type: NpmTask) {
  args = ['run', 'e2e']
  dependsOn compileJava, buildUI, startRest
}

task e2eUI {
  dependsOn tasks.e2eLaunch
}

task stopBootrun {
  doLast {
    println("STOPPING Spring Boot")
    def connection = new URL("http://localhost:8080/shutdown").openConnection()
    connection.setRequestMethod("POST")
    connection.connect()
    println 'Shutting down application...'
    println connection.inputStream.text
  }
}

e2eUI.finalizedBy stopBootrun

if (!(
  gradle.startParameter.taskNames*.toLowerCase().contains("bootrun") ||
    gradle.startParameter.taskNames*.toLowerCase().contains("test") ||
    gradle.startParameter.taskNames*.toLowerCase().contains("publishplugins") ||
    gradle.startParameter.taskNames*.toLowerCase().contains("bintrayUpload") ||
    gradle.startParameter.taskNames*.toLowerCase().contains("javadoc") ||
    project.hasProperty('skipui')
)
) {
  processResources.dependsOn copyIndexHtml
}
publishing {
  publications {
    if(!project.hasProperty('skipWeb')) {
      mavenWeb(MavenPublication) {
        group = "com.marklogic"
        version = "${version}"
        from components.web

      }
    }
  }
  repositories {
    maven {
      if(project.hasProperty("mavenUser")) {
        credentials {
          username mavenUser
          password mavenPassword
        }
      }
      url publishUrl
    }
  }
}

//removing test dependency on e2e tests
//test.dependsOn e2eUI

bootRun {
  environment 'spring.profiles.active', 'dev'
}

springBoot {
  // not in spring boot 2 executable = true
  mainClassName = "com.marklogic.hub.web.WebApplication"
  buildInfo {
    properties {
      group = "marklogic"
      name = "marklogic-datahub"
      version = "${version}"
    }
  }
}

test {
  exclude 'com/marklogic/hub/web/integrationtests/**'
  useJUnitPlatform()
}
