buildscript {
    repositories {
        jcenter()
        maven {url 'http://developer.marklogic.com/maven2/'}
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE')
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'com.eriwen.gradle.js' version '1.12.1'
}

apply plugin: 'spring-boot'

repositories {
    jcenter()
    maven {url 'http://developer.marklogic.com/maven2/'}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    providedRuntime
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile project(':marklogic-data-hub')
    compile('io.springfox:springfox-swagger2:2.4.0')
    compile('org.webjars.bower:angular-swagger-ui:0.3.1')

    // Needed for the Upload feature using mlcp
    compile('com.marklogic:mlcp-util:0.2.0') {
        exclude module : 'servlet-api'
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile 'com.google.guava:guava:19.0'

    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
    testCompile('junit:junit')
}


javascript.source {
    dev {
        js {
            srcDir "src/main/resources/static"
            include "**/*.js"
            exclude "lib/**/*"
        }
    }
}

jshint {
    tasks.jshint.source = javascript.source.dev.js.files
    tasks.jshint.dest = file("${buildDir}/jshint.out")
    tasks.jshint.ignoreExitCode = false
    tasks.jshint.outputToStdOut = true
    tasks.jshint.reporter = 'checkstyle'
    options = ["--config":"src/main/resources/static/.jshintrc"]
}


test.dependsOn tasks.jshint
