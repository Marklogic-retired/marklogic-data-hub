plugins {
    id 'java-library'
    id 'maven-publish'

    id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

group = 'com.marklogic'

// See https://github.com/snyk/gradle-plugin for docs
snyk {
    severity = 'medium'
    api = snykToken
    autoDownload = true
    autoUpdate = true
}

dependencies {
    api(project(":marklogic-data-hub-api"))

    // Because one of the main features of the test helper classes in this project is access to a Logger object,
    // this is an API dependency.
    api "org.slf4j:slf4j-api:1.7.31"

    // This is an API dependency due to the test methods that return a class that depends on a JDOM2 class
    api "org.jdom:jdom2:2.0.6"

    api "com.fasterxml.jackson.core:jackson-databind:2.11.1"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.11.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allJava
}

publishing {
    publications {
        mainJava(MavenPublication) {
            from components.java
        }
        sourcesJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            if (project.hasProperty("mavenUser")) {
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
            url publishUrl
        }
    }
}
