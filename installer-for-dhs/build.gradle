plugins {
    id "java"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "io.snyk.gradle.plugin.snykplugin" version "0.4"
    id 'maven-publish'
}

group = 'com.marklogic'

// See https://github.com/snyk/gradle-plugin for docs
snyk {
    severity = 'medium'
    api = snykToken
    autoDownload = true
    autoUpdate = true
}

dependencies {
    implementation project(":marklogic-data-hub")
    implementation "com.beust:jcommander:1.72"
    implementation 'commons-io:commons-io:2.4'
    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation(testFixtures(project(":marklogic-data-hub")))
}

test {
    systemProperty "mlHost", mlHost
    useJUnitPlatform()
}

shadowJar {
    archiveBaseName = "marklogic-data-hub"
    archiveClassifier = "installer"
    manifest {
        attributes "Main-Class": "com.marklogic.hub.dhs.installer.Main"
    }
}
build.dependsOn shadowJar

task installDhfIntoDhs(type: Exec) {
    description = "Builds and runs the DHF installer for DHS"
    workingDir = "."
    commandLine = "java"
    args = [
        "-jar", "build/libs/marklogic-data-hub-${version}-installer.jar",
        "--path", "build/dhsInstaller",
        "--host", mlHost,
        "--username", mlUsername,
        "--password", mlPassword,
        "dhsInstall"
    ]
}
installDhfIntoDhs.dependsOn jar


publishing {
    publications {
        maven(MavenPublication) {
            artifact("build/libs/marklogic-data-hub-$version-installer"+".jar")
            artifactId = 'installer-for-dhs'
        }
    }
    repositories {
        maven {
            if (project.hasProperty("mavenUser")) {
                credentials {
                    username mavenUser
                    password mavenPassword
                }
            }
            url publishUrl
        }
    }

}



